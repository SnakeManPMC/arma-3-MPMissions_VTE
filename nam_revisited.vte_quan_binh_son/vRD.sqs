;-----------------------------------------------------------------------------
;Respawn or Delete Vehicle
;by «WärWolf» / ArMoGaDoN
;
;always spawns at same place and direction it started life
;respawn times settable in-script (by class-type)
;
;init line of each vehicle to use script:
;[this,"vehicleName",{true || false}] exec "ArMo\vRD.sqs"
;where true = respawn, false=delete
;Game Logic 'Server' needed on map to stop multi-spawn
;-----------------------------------------------------------------------------
_vcl=_this select 0;
_vclName=_this select 1;
_willRespawn=_this select 2;
?!(local Server):goto "end";
_respawnpoint=getPos _vcl;
_azimut=getDir _vcl;
_class=typeOf _vcl;
_isAH64=false;_isAH64BLK=false;
;(_R=respawn _D=delete  ... delays)
_Rcar=120;
_Rbike=120;
_Rtruckjeep=120;
_Rengineering=120;
_RAPC=180;
_Rmaintank=240;
_RATKH=240;
_RUH=180;
_RMH=120;
_Rplane=300;
_Rboat=120;
_Dallvehicles=180;
;leave default _RDelay set at 200, use others above to set delays you want
_Rdelay=200;
;using a random delay+loop here instead of an @wait command gives less lag
_rnd = (random 8) + 4;
#start
~(_rnd)
?(end1):exit;
?(canMove _vcl||canFire _vcl):goto "start";
;check inhabitants AFTER loop
@(isnull driver _vcl)
?(_willRespawn):goto "respawn";
;#delete = if not respawning then delay until time to delete
~(_DallVehicles)
deleteVehicle _vcl;
goto "end";
#respawn
?(_class in ["MAP_AH64","MAP_AH64_CO","MAP_AH64_CC","MAP_AH64_GS","MAP_AH64_GS_M229","MAP_AH64_GS_Mixed","MAP_AH64_MAV","MAP_AH64_MAV2","MAP_AH64_AA","MAP_AH64_TOW"]):_Rdelay=_RATKH;_isAH64=true;goto "r2";
?(_class in ["MAP_AH64_BLK","MAP_AH64_CO_BLK","MAP_AH64_CC_BLK","MAP_AH64_GS_BLK","MAP_AH64_GS_M229_BLK","MAP_AH64_GS_Mixed_BLK","MAP_AH64_TOW_BLK"]):_Rdelay=_RATKH;_isAH64BLK=true;goto "r2";
?(_class in ["MAP_AV8B_MAV"]):_Rdelay=_Rplane;goto "r2";
?(_class in ["MAP_CH47D","MAP_CH47D_Cargo","MAP_CH47D_Medivac"]):_Rdelay=_RUH;goto "r2";
?(_class in ["RHS_Mi24D","RHS_Mi24P_AT","RHS_Mi24P_Cas","RHS_Mi24V_CAS","RHS_Mi24V_AT","AH1Z","AH6J_EP1","AH6J_EP1","Ka52"]):_Rdelay=_RATKH;goto "r2";
?(_class in ["MH60S","UH60M_EP1","Mi17_rockets_RU"]):_Rdelay=_RUH;goto "r2";
?(_class in ["AH6J_EP1","AH6J_EP1","Mi17_rockets_RU"]):_Rdelay=_RMH;goto "r2";
?(_class in ["A10","AV8B2","AV8B2","Camel","Camel2","Su34","Su34B"]):_Rdelay=_Rplane;goto "r2";
?(_class in ["vte_pbr","vte_pbr","vte_pbr","vte_pbr","vte_pbr"]):_Rdelay=_Rboat;goto "r2";
?(_class in ["MTVR","MTVR","MTVR","VTE_nvatruck","VTE_nvatruckopen","VTE_nvatruck","VTE_nvatruck","HMMWV_M2","HMMWV_TOW","HMMWV_MK19","HMMWV","vte_nvaUAZMG","UAZ_AGS30_RU","vte_nvauaz","vte_zsu57"]):_Rdelay=_Rtruckjeep;goto "r2";
?(_class in ["Skoda","SkodaBlue","SkodaRed","SkodaGreen","datsun1_civil_1_open","vte_jeep","datsun1_civil_3_open","car_hatchback","car_sedan","HMMWV_M2","LandRover_TK_CIV_EP1","LandRover_TK_CIV_EP1","Bus","tractor","hilux1_civil_1_open","hilux1_civil_2_covered","hilux1_civil_1_open"]):_Rdelay=_Rcar;goto "r2";
?(_class in ["vte_jeep","TT650G","TT650C","Bicycle"]):_Rdelay=_Rbike;goto "r2";
?(_class in ["VTE_truck5tusrepair","VTE_truck5tusammo","VTE_truck5tusfuel","VTE_nvatruckrepair","VTE_nvatruckammo","VTE_nvatruckfuel"]):_Rdelay=_Rengineering;goto "r2";
?(_class in ["M1126_ICV_M2_EP1","M1126_ICV_mk19_EP1","M1135_ATGMV_EP1","BRDM2_INS","BRDM2_ATGM_INS","M2A2_EP1","M2A2_EP1","VTE_m113Ambul","M2A2_EP1","M2A2_EP1","vte_pt76","vte_pt76"]):_Rdelay=_RAPC;goto "r2";
?(_class in ["M1A2_TUSK_MG","T72_RU"]):_Rdelay=_Rmaintank;
#r2
~(_Rdelay)
;check inhabitants again
#chkEmpty
~1
?!(isnull driver _vcl):goto "chkEmpty";
#r3
~(_rnd/4)
?(bRHold):goto "r3";
bRHold=true;
_vcl removeAllEventHandlers "GetIn";
deleteVehicle _vcl;
?(_isAH64):_tObj=_respawnpoint nearestObject "Land_MAP_AH64_Wrack";if(!isNull _tObj)then{deleteVehicle _tObj;};
?(_isAH64BLK):_tObj=_respawnpoint nearestObject "Land_MAP_AH64_Wrack_BLK";if(!isNull _tObj)then{deleteVehicle _tObj;};
vclRspwn=_class createVehicle _respawnpoint;vclRspwn setVehicleVarName _vclName;
_vcl=vclRspwn;_vcl setdir _azimut;_vcl setPos _respawnpoint;_vcl setvelocity [0,0,0];_vcl lock false;
?(CrewCheckEnabled):bRspwn=true;publicVariable "vclRspwn";publicVariable "bRspwn";
~7
bRHold=false;
goto "start";
#end
exit;

