; 
; Spectating script v1.0 by Kegetys <Kegetys@raketti.net> 
; http://www.ofp.kege.cjb.net
;
; Requires OFP Version 1.75 or up
;


;
; Change DeathCamArray values to spectateable objects
; Remember to use driver/gunner if you want to spectate the player driving/gunning the vehicle if he exits it
;


DeathCamArray = units ops





_seagull = _this select 2
_seagull setpos[0,0,0]

DeathCamCurCamTarget = "Land_VTE_bis_Heli_H" CreateVehicle getpos player

&1.5
titleCut ["","BLACK OUT", 1.5]
&3
titleCut ["","BLACK IN", 4]

DeathCamCineViewSpecial = 0
DeathCamComSpeed = 0.4
DeathCamRelPos = [0,-2,0.3]
DeathCamTarHeight = 1.7
DeathCamIndex = 0

DeathCamCurTarget = player
DeathCam = "camera" camCreate [(getpos player select 0)+2, (getpos player select 1)+2, 700 ]
DeathCam cameraEffect ["internal","front"]
DeathCam camSetTarget vehicle DeathCamCurCamTarget
DeathCam camSetPos [(getpos player select 0)+2, (getpos player select 1)+2, 1000 ]
DeathCam camSetFOV 0.976
DeathCam camCommand "inertia on"
DeathCam camCommit 0
showCinemaBorder false
_ok = createDialog "DlgSpectateDeath"
ctrlShow [1046001,false];ctrlShow [1046002,false];ctrlShow [1046003,false];ctrlShow [1046004,false];ctrlShow [1046005,false];ctrlShow [1046006,false];ctrlShow [1046007,false];ctrlShow [1046008,true];ctrlShow [1046009,false]
ctrlSetText [116969,name player]

? not _ok : exit

#loop

DeathCam camCommit DeathCamComSpeed
_vhlTemp = vehicle DeathCamCurTarget

; Cinematic view?
? DeathCamCineViewSpecial == 1 : goto "camspecial"




; The following will reduce the effect of warping spectated object to the camera itself

_Divider = 100 - (speed _vhlTemp * 2)
? _Divider < 10 : _Divider = 10
_moX = (getpos DeathCamCurCamTarget select 0) + (((getpos _vhlTemp select 0) - (getpos DeathCamCurCamTarget select 0))/_Divider)
_moY = (getpos DeathCamCurCamTarget select 1) + (((getpos _vhlTemp select 1) - (getpos DeathCamCurCamTarget select 1))/_Divider)
_moZ = ((getpos DeathCamCurCamTarget select 2)-DeathCamTarHeight) + (((getpos _vhlTemp select 2) - ((getpos DeathCamCurCamTarget select 2)-DeathCamTarHeight))/_Divider)

DeathCamCurCamTarget setpos[_moX,_moY,_moZ  + DeathCamTarHeight]

DeathCamCurCamTarget setdir getdir _vhlTemp

; Top view?
? DeathCamCineViewSpecial == 2 : goto "camspecialTop"


_RealRelPos = DeathCamRelPos
? ((speed vehicle DeathCamCurTarget >= 150)) : _RealRelPos = [(DeathCamRelPos select 0)*10, (DeathCamRelPos select 1)*10, (DeathCamRelPos select 2)*10]

DeathCam camSetRelPos _RealRelPos 


goto "loop"


; Cinematic view
#camspecial
DeathCamCurCamTarget setpos[getpos _vhlTemp select 0,getpos _vhlTemp select 1,(getpos _vhlTemp select 2) + DeathCamTarHeight]
DeathCamCurCamTarget setdir getdir _vhlTemp

; Reset cam if object is far away... if its a very fast object use a more distant view
? ((speed vehicle DeathCamCurTarget >= 150)) and (((DeathCam) distance (DeathCamCurCamTarget)) > 400) : DeathCam camSetRelPos [3,400.1,10.0]
? ((speed vehicle DeathCamCurTarget < 150)) and (((DeathCam) distance (DeathCamCurCamTarget)) > 100) : DeathCam camSetRelPos DeathCamRelPos

goto "loop"

; Top view
#camspecialTop
DeathCamCurCamTarget setdir 0
DeathCam camSetPos [getpos DeathCamCurCamTarget select 0,getpos DeathCamCurCamTarget select 1,(getpos DeathCamCurCamTarget select 2)+(20 + (speed vehicle DeathCamCurTarget / 2))]

goto "loop"

